#Область ПрограммныйИнтерфейс

//Находит действующие в указанном периоде договоры
//Параметры:
//Параметры - Структура
//АдресРезультата - УникальныйИдентификатор
Процедура ВКМ_НайтиДоговорыДляЗаполнения(Параметры, АдресРезультата)Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ДоговорыКонтрагентов.Владелец.Ссылка КАК Контрагент,
	|	ДоговорыКонтрагентов.ВКМ_СуммаАбонентскойПлаты КАК СуммаДоговора,
	|	ДоговорыКонтрагентов.ВКМ_СтоимостьЧасаРаботы КАК СтоимостьЧасаРаботы,
	|	ЕСТЬNULL(РеализацияТоваровУслуг.Ссылка, ЗНАЧЕНИЕ(Документ.РеализацияТоваровУслуг.ПустаяСсылка)) КАК Реализация,
	|	ДоговорыКонтрагентов.Организация КАК Организация
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|		ПО (ДоговорыКонтрагентов.Ссылка = РеализацияТоваровУслуг.Основание)
	|		И (РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода)
	|		И (РеализацияТоваровУслуг.Проведен)
	|ГДЕ
	|	ДоговорыКонтрагентов.ВидДоговора.Ссылка = &ВидДоговора
	|	И ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора <= &КонецПериода
	|	И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора >= &НачалоПериода";
	
	
    Запрос.УстановитьПараметр("НачалоПериода", Параметры.ДатаНачала);
	Запрос.УстановитьПараметр("ВидДоговора", Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонетскоеОбслуживание);
	Запрос.УстановитьПараметр("КонецПериода", Параметры.ДатаОкончания);
	ТаблицаРезультат = Запрос.Выполнить().Выгрузить();
	
	ПоместитьВоВременноеХранилище(ТаблицаРезультат, АдресРезультата);
	
КонецПроцедуры


//Создает и проводит Документы Реализации по договорам
//Параметры:
//Параметры - Структура
//АдресРезультата - УникальныйИдентификатор
Процедура ВКМ_СоздатьРеализацииКДоговорам(Параметры, АдресРезультата) Экспорт
	ТаблицаДанных = Параметры.Таблица ;
	ДокументовСоздано = 0;
	ДокументовИзменено = 0;
	ТекстСообщения = "";

	Для Каждого СтрокаТЧ Из ТаблицаДанных Цикл
		Если СтрокаТЧ.Реализация = Документы.РеализацияТоваровУслуг.ПустаяСсылка() Тогда
			НовыйДокумент = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
			НовыйДокумент.Заполнить(СтрокаТЧ.Договор);
			НовыйДокумент.Дата = Параметры.ДатаОкончания;
			НовыйДокумент.ВКМ_ВыполнитьЗаполнение();
			НовыйДокумент.Комментарий = "##Выполнено обработкой Массовое создание реализаций##";
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение, );
			
			СтрокаТЧ.Реализация = НовыйДокумент.Ссылка;
			
			ДокументовСоздано = ДокументовСоздано + 1;
		Иначе
			ДокТЧ = СтрокаТЧ.Реализация.ПолучитьОбъект();
			ДокТЧ.Заполнить(СтрокаТЧ.Договор);
			ДокТЧ.Дата = Параметры.ДатаОкончания;
			ДокТЧ.ВКМ_ВыполнитьЗаполнение();
			ДокТЧ.Комментарий = ДокТЧ.Комментарий + "##Изменено обработкой Массовое создание реализаций";
			ДокТЧ.Записать(РежимЗаписиДокумента.Проведение, );
		КонецЕсли;
	КонецЦикла;
	
	Если ДокументовСоздано > 0 Тогда
		ТекстСообщения = СтрШаблон("Успешно создано %1 документов!", ДокументовСоздано);
	КонецЕсли;
	Если ДокументовИзменено > 0 Тогда
		ТекстСообщения = ТекстСообщения + СтрШаблон("Изменено %1 документов!", ДокументовИзменено);
	КонецЕсли;
	
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	ПоместитьВоВременноеХранилище(ТаблицаДанных, АдресРезультата);
	

КонецПроцедуры

#КонецОбласти
