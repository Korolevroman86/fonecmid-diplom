
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции


Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
	
КонецПроцедуры

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ВКМ_АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 5;
	
КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "ВКМ_АктОбОказанныхУслугах");
	Если ПечатнаяФорма <> Неопределено Тогда
	    ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
	    ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
	    ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОбОказанныхУслугах";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ВКМ_АктОбОказанныхУслугах";
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОбОказанныхУслугах");
	
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	
	ПервыйДокумент = Истина;
	
	Пока ДанныеДокументов.Следующий() Цикл
		
		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиПоляАкта(ДанныеДокументов, ТабличныйДокумент, Макет);

		// В табличном документе необходимо задать имя области, в которую был 
		// выведен объект. Нужно для возможности печати комплектов документов.
        УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, 
            НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);		
		
	КонецЦикла;	
		
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Договор КАК Договор,
	|	РеализацияТоваровУслуг.Услуги.(
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество,
	|		Сумма КАК Сумма,
	|		Цена) КАК Услуги,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваровУслуг.Организация.ИНН КАК ОрганизацияИНН,
	|	РеализацияТоваровУслуг.Организация.КПП КАК ОрганизацияКПП,
	|	РеализацияТоваровУслуг.Организация.Представление КАК ОрганизацияПредставление,
	|	РеализацияТоваровУслуг.Контрагент.ИНН КАК КонтрагентИНН,
	|	РеализацияТоваровУслуг.Контрагент.КПП КАК КонтрагентКПП,
	|	РеализацияТоваровУслуг.Контрагент.Представление КАК КонтрагентПредставление
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	 
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

Процедура ВывестиПоляАкта(ДанныеДокументов, ТабличныйДокумент, Макет)
	
	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	ОбластьШапка = Макет.ПолучитьОбласть("Шапка");
	ОбластьТабличнаяЧастьШапка = Макет.ПолучитьОбласть("ШапкаТЧ");
    ОбластьТабличнаяЧастьСтроки = Макет.ПолучитьОбласть("СтрокаТЧ");
	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");
	
	
	ДанныеПечати = Новый Структура;
	ШаблонЗаголовка = "Акт об оказанных услугах № %1 от %2";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка,
	ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
	Формат(ДанныеДокументов.Дата, "ДЛФ=D"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);	
	ОбластьЗаголовок.Параметры.Заполнить(ДанныеПечати); 
	
	Реквизиты = новый Структура;
	Реквизиты.Вставить("Организация", ДанныеДокументов.ОрганизацияПредставление);
	Реквизиты.Вставить("Контрагент", ДанныеДокументов.КонтрагентПредставление);
	Реквизиты.Вставить("ОрганизацияИНН",ДанныеДокументов.ОрганизацияИНН);
	Реквизиты.Вставить("ОрганизацияКПП",ДанныеДокументов.ОрганизацияКПП);
	Реквизиты.Вставить("КонтрагентИНН",ДанныеДокументов.КонтрагентИНН);
	Реквизиты.Вставить("КонтрагентКПП",ДанныеДокументов.КонтрагентКПП);
	Реквизиты.Вставить("СуммаДокумента", ДанныеДокументов.СуммаДокумента);
	Реквизиты.Вставить("СуммаПрописью", ЧислоПрописью(ДанныеДокументов.СуммаДокумента,"Л = ru_RU; ДП = Истина" ,"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"));
	ОбластьШапка.Параметры.Заполнить(Реквизиты);
	
	ОбластьПодвал.Параметры.Заполнить(Реквизиты);
	
	ТабличныйДокумент.Вывести(ОбластьЗаголовок);	
	ТабличныйДокумент.Вывести(ОбластьШапка);
	ТабличныйДокумент.Вывести(ОбластьТабличнаяЧастьШапка);
	
	ВыборкаУслуги = ДанныеДокументов.Услуги.Выбрать();
	Пока ВыборкаУслуги.Следующий() Цикл
		ОбластьТабличнаяЧастьСтроки.Параметры.Заполнить(ВыборкаУслуги);
		ТабличныйДокумент.Вывести(ОбластьТабличнаяЧастьСтроки);
	КонецЦикла;
	
	ТабличныйДокумент.Вывести(ОбластьПодвал);
		
КонецПроцедуры



#КонецОбласти


#КонецЕсли

