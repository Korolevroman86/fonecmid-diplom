#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьТаблицу(Команда)
	Если Не ЗначениеЗаполнено(Объект.Период) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Для выполнения команды необходимо заполнить период!");
		Возврат;
	КонецЕсли;
	ВыполнитьКоманду(Команда.Имя);
КонецПроцедуры


&НаКлиенте
Процедура СоздатьРеализации(Команда)
	
	Если Объект.Договора.Количество()<1 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Для выполнения команды необходимо ""Заполнить Таблицу""");
		Возврат;
	КонецЕсли;
	ВыполнитьКоманду(Команда.Имя);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьКоманду(Команда);
	
	ДлительнаяОперация = НачатьВыполнениеНаСервере(Команда);
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьДействиеЗавершение", ЭтотОбъект);
    ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
	
КонецПроцедуры

&НаСервере
Функция НачатьВыполнениеНаСервере(Команда);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыПроцедуры = Новый Структура;
	Если Команда = "СоздатьРеализации" Тогда
		Таблица = Объект.Договора.Выгрузить();
	    ПараметрыПроцедуры.Вставить("Таблица", Таблица);
	    
	    Если Объект.Период.ДатаОкончания > ТекущаяДатаСеанса() Тогда
	    	ДатаОкончания = ТекущаяДатаСеанса();
	    Иначе
	    	ДатаОкончания = Объект.Период.ДатаОкончания;
	    КонецЕсли;
	    
	    ПараметрыПроцедуры.Вставить("ДатаОкончания", ДатаОкончания);
	Возврат ДлительныеОперации.ВыполнитьВФоне("ВКМ_МассовоеСозданиеРеализаций.ВКМ_СоздатьРеализацииКДоговорам", 
        ПараметрыПроцедуры, ПараметрыВыполнения);
	
	КонецЕсли;
		
	Если Команда = "ЗаполнитьТаблицу" Тогда
	ПараметрыПроцедуры.Вставить("ДатаНачала", Объект.Период.ДатаНачала);
	ПараметрыПроцедуры.Вставить("ДатаОкончания", Объект.Период.ДатаОкончания);
	
    Возврат ДлительныеОперации.ВыполнитьВФоне("ВКМ_МассовоеСозданиеРеализаций.ВКМ_НайтиДоговорыДляЗаполнения", 
            ПараметрыПроцедуры, ПараметрыВыполнения);
	КонецЕсли;
    
 КонецФункции   


//Заполняет табличную часть обработки
//Параметры:
//Результат - Структура
&НаСервере
Процедура ВКМ_ЗаполнитьТаблицуНаСервере(Результат) 
	Объект.Договора.Очистить();
	Таблица = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	Если Таблица.Количество() > 0 Тогда
		
		Объект.Договора.Загрузить(Таблица);	
	Иначе 
	    ОбщегоНазначения.СообщитьПользователю("За выбранный период нет активных договоров абонентского обслуживания");	
	КонецЕсли;
	    
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
     Если Результат = Неопределено Тогда
      Возврат;
     КонецЕсли;
     ВКМ_ЗаполнитьТаблицуНаСервере(Результат);
КонецПроцедуры

#КонецОбласти
