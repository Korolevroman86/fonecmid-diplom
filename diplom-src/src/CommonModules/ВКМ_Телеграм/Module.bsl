

#Область СлужебныйПрограммныйИнтерфейс 


Процедура ВКМ_РассылкаСообщенийТелеграм() Экспорт
	ПроизвестиРассылку();
КонецПроцедуры
 


#КонецОбласти  

#Область СлужебныеПроцедурыИФункции

//Формирует Настройки для рассылки 
Процедура ПроизвестиРассылку() 
	
	МассивДанных = ПолучитьМассивДанных();
	
	Если МассивДанных.Количество() < 1 Тогда
		Возврат 
	КонецЕсли;
	
	
	ТокенУправленияБотом = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	
	ИдентификаторГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещенияТелеграм.Получить();
	
	Настройки = Новый  Структура; 
		
	ТипСоединения =  Новый ЗащищенноеСоединениеOpenSSL;  
		
	Адрес = "api.telegram.org";
	
	Сервис = "/bot" + ТокенУправленияБотом + "/sendMessage";
	
	
	Настройки.Вставить("ТипСоединения", ТипСоединения);  
	Настройки.Вставить("Адрес", Адрес);  
	
	Соединение = УстановкаСоединения(Настройки);
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json");
	
	Настройки.Вставить("Сервис", Сервис);
	Настройки.Вставить("Соединение", Соединение); 
	Настройки.Вставить("Заголовки", Заголовки);
		
	Рассылка = Новый Структура;
	Рассылка.Вставить("chat_id", ИдентификаторГруппы);
	
	Для Каждого СтрокаДанных Из МассивДанных Цикл
	
	    Рассылка.Вставить("text", СтрокаДанных.ТекстСообщения);
	
	    ТелоЗапросаJson = КонвертацияДанныхОбмена(Рассылка);
	
	    Настройки.Вставить("ТелоЗапроса", ТелоЗапросаJson);
	    Настройки.Вставить("Ссылка", СтрокаДанных.Ссылка);
	
	    ОтправкаЗапроса(Настройки);
	
	КонецЦикла;	
	
КонецПроцедуры

//Создает HTTP Соединение
//Параметры - Настройки - структура.
//ВозвращаемоеЗначение: HTTPСоединение
Функция УстановкаСоединения(Настройки) 	
	Соединение = Новый  HTTPСоединение(Настройки.Адрес,443,,,,,Настройки.ТипСоединения);
	Возврат Соединение
КонецФункции 

//Получает данные о новых сообщениях для рассылки и ссылки на объекты
Функция ПолучитьМассивДанных()
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБотуДляОтправки.Ссылка,
	|	ВКМ_УведомленияТелеграмБотуДляОтправки.ТекстСообщения
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБотуДляОтправки КАК ВКМ_УведомленияТелеграмБотуДляОтправки
	|ГДЕ
	|	НЕ ВКМ_УведомленияТелеграмБотуДляОтправки.ПометкаУдаления";
	Выборка = Запрос.Выполнить().Выгрузить();
	Возврат Выборка

КонецФункции


//Возвращает СтрокуJson
//Параметры - СтруктураДанных - значения строки 
//ВозвращаемоеЗначение: Строка
Функция КонвертацияДанныхОбмена(СтруктураДанных) 
	
	Файл = Новый ЗаписьJSON; 
	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);
	Файл.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Файл, СтруктураДанных); 
	JSONСтрока = Файл.Закрыть();
	Возврат JSONСтрока 
	
КонецФункции


//Формирует Запрос, получает ответ, удаляет ссылки на элементы справочника
Процедура ОтправкаЗапроса(Настройки)
	
	Запрос = Новый  HTTPЗапрос(Настройки.Сервис, Настройки.Заголовки);
	
	Запрос.УстановитьТелоИзСтроки(Настройки.ТелоЗапроса);
	
	Отправка = Настройки.Соединение.ВызватьHTTPМетод("POST", Запрос);	 
	
	ОтветСервера = Отправка.ПолучитьТелоКакСтроку(); 
	   
	Если Отправка.КодСостояния <> 200 Тогда 
		ЗаписьЖурналаРегистрации("ОтправкаСообщенияВГруппуТелеграм",УровеньЖурналаРегистрации.Ошибка,, Настройки.Ссылка, ОтветСервера);
		Возврат;
	Иначе
	
		Объект = Настройки.Ссылка.ПолучитьОбъект();
		Объект.Удалить();
	
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти  



