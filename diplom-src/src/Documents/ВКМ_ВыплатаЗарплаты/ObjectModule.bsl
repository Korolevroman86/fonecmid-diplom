#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда


#Область ОбработчикиСобытий
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник КАК Сотрудник,
	|	ВКМ_ВыплатаЗарплатыВыплаты.Сумма КАК Сумма,
	|	ЕСТЬNULL(ВКМ_ВзаиморасчетыССотрудникамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|	ВКМ_ВыплатаЗарплатыВыплаты.НомерСтроки КАК НомерСтроки,
	|	ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник.Представление КАК СотрудникПредставление
	|ИЗ
	|	Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВКМ_ВзаиморасчетыССотрудниками.Остатки(&Дата, Сотрудник В
	|			(ВЫБРАТЬ
	|				ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник
	|			ИЗ
	|				Документ.ВКМ_ВыплатаЗарплаты.Выплаты КАК ВКМ_ВыплатаЗарплатыВыплаты)) КАК ВКМ_ВзаиморасчетыССотрудникамиОстатки
	|		ПО ВКМ_ВыплатаЗарплатыВыплаты.Сотрудник = ВКМ_ВзаиморасчетыССотрудникамиОстатки.Сотрудник
	|ГДЕ
	|	ВКМ_ВыплатаЗарплатыВыплаты.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Дата",КонецДня(Дата));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.СуммаОстаток < Выборка.Сумма Тогда
			
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Сумма оплаты для %1 превышает сумму остатка к выплате, остаток %2", Выборка.СотрудникПредставление, Выборка.СуммаОстаток),
			           ЭтотОбъект, "Выплаты[" +(Выборка.НомерСтроки - 1) + "].Сумма", "Объект", Отказ);
		Продолжить;
		КонецЕсли;	
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.Сотрудник = Выборка.Сотрудник;
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Сумма = Выборка.Сумма;

	КонецЦикла;
	

КонецПроцедуры

#КонецОбласти



#КонецЕсли